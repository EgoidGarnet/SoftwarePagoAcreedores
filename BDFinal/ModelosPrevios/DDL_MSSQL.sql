-- =====================================================
-- Crear base de datos y usarla
-- =====================================================
IF DB_ID(N'softpacbd') IS NULL
    CREATE DATABASE [softpacbd];
GO

USE [softpacbd];
GO

-- =====================================================
-- Tabla dbo.PA_USUARIOS
-- =====================================================
IF OBJECT_ID(N'dbo.PA_USUARIOS', N'U') IS NOT NULL
    DROP TABLE dbo.PA_USUARIOS;
GO

CREATE TABLE dbo.PA_USUARIOS (
    USUARIO_ID INT IDENTITY(1,1) NOT NULL,
    CORREO_ELECTRONICO VARCHAR(45) NOT NULL,
    NOMBRE_DE_USUARIO VARCHAR(45) NOT NULL,
    NOMBRE VARCHAR(45) NULL,
    APELLIDOS VARCHAR(45) NULL,
    ACTIVO CHAR(1) NOT NULL,
    PASSWORD_HASH VARCHAR(72) NOT NULL,
    SUPERUSUARIO CHAR(1) NOT NULL CONSTRAINT DF_PA_USUARIOS_SUPERUSUARIO DEFAULT ('N'),
    FECHA_ELIMINACION DATETIME2 NULL,
    USUARIO_ELIMINACION INT NULL,
    CONSTRAINT PK_PA_USUARIOS PRIMARY KEY (USUARIO_ID),
    CONSTRAINT UQ_PA_USUARIOS_NOMBRE_DE_USUARIO UNIQUE (NOMBRE_DE_USUARIO),
    CONSTRAINT FK_PA_USUARIOS_USUARIO_ELIMINACION FOREIGN KEY (USUARIO_ELIMINACION)
        REFERENCES dbo.PA_USUARIOS (USUARIO_ID)
);
GO

-- =====================================================
-- Tabla dbo.PA_PAISES
-- =====================================================
IF OBJECT_ID(N'dbo.PA_PAISES', N'U') IS NOT NULL
    DROP TABLE dbo.PA_PAISES;
GO

CREATE TABLE dbo.PA_PAISES (
    PAIS_ID            INT          IDENTITY(1,1) NOT NULL,
    NOMBRE             VARCHAR(60)  NOT NULL,
    CODIGO_ISO         CHAR(2)      NOT NULL,
    CODIGO_TELEFONICO  CHAR(3)      NULL,
    CONSTRAINT PK_PA_PAISES PRIMARY KEY (PAIS_ID),
    CONSTRAINT UQ_PA_PAISES_NOMBRE 
        UNIQUE (NOMBRE),
    CONSTRAINT UQ_PA_PAISES_CODIGO_ISO 
        UNIQUE (CODIGO_ISO)
);
GO

-- =====================================================
-- Tabla dbo.PA_USUARIO_PAIS_ACCESO
-- =====================================================
IF OBJECT_ID(N'dbo.PA_USUARIO_PAIS_ACCESO', N'U') IS NOT NULL
    DROP TABLE dbo.PA_USUARIO_PAIS_ACCESO;
GO

CREATE TABLE dbo.PA_USUARIO_PAIS_ACCESO (
    USUARIO_ID INT      NOT NULL,
    ACCESO     CHAR(1)  NOT NULL,
    PAIS_ID    INT      NOT NULL,
    CONSTRAINT PK_PA_USUARIO_PAIS_ACCESO 
        PRIMARY KEY (USUARIO_ID, PAIS_ID),
    CONSTRAINT FK_PA_USUARIO_PAIS_ACCESO_PAIS
        FOREIGN KEY (PAIS_ID)
        REFERENCES dbo.PA_PAISES (PAIS_ID),
    CONSTRAINT FK_PA_USUARIO_PAIS_ACCESO_USUARIO
        FOREIGN KEY (USUARIO_ID)
        REFERENCES dbo.PA_USUARIOS (USUARIO_ID)
);
GO

-- =====================================================
-- Tabla dbo.PA_ACREEDORES
-- =====================================================
IF OBJECT_ID(N'dbo.PA_ACREEDORES', N'U') IS NOT NULL
    DROP TABLE dbo.PA_ACREEDORES;
GO

CREATE TABLE dbo.PA_ACREEDORES (
    ACREEDOR_ID        INT           IDENTITY(1,1) NOT NULL,
    RAZON_SOCIAL       VARCHAR(200)  NOT NULL,
    RUC                CHAR(13)      NOT NULL,
    DIRECCION_FISCAL   VARCHAR(200)  NOT NULL,
    CONDICION          VARCHAR(100)  NULL,
    PLAZO_DE_PAGO      INT           NOT NULL,
    ACTIVO             CHAR(1)       NOT NULL CONSTRAINT DF_PA_ACREEDORES_ACTIVO DEFAULT ('S'),
    PAIS_ID            INT           NOT NULL,
    FECHA_ELIMINACION  DATETIME2     NULL,
    USUARIO_ELIMINACION INT          NULL,

    CONSTRAINT PK_PA_ACREEDORES PRIMARY KEY (ACREEDOR_ID),
    CONSTRAINT UQ_PA_ACREEDORES_RUC UNIQUE (RUC),

    CONSTRAINT FK_PA_ACREEDORES_PA_PAISES
        FOREIGN KEY (PAIS_ID)
        REFERENCES dbo.PA_PAISES (PAIS_ID),

    CONSTRAINT FK_PA_ACREEDORES_PA_USUARIOS
        FOREIGN KEY (USUARIO_ELIMINACION)
        REFERENCES dbo.PA_USUARIOS (USUARIO_ID)
);
GO

-- =====================================================
-- Tabla dbo.PA_ENTIDADES_BANCARIAS
-- =====================================================
IF OBJECT_ID(N'dbo.PA_ENTIDADES_BANCARIAS', N'U') IS NOT NULL
    DROP TABLE dbo.PA_ENTIDADES_BANCARIAS;
GO

CREATE TABLE dbo.PA_ENTIDADES_BANCARIAS (
    ENTIDAD_BANCARIA_ID INT          IDENTITY(1,1) NOT NULL,
    NOMBRE              VARCHAR(100) NOT NULL,
    FORMATO_ACEPTADO    VARCHAR(50)  NOT NULL,
    CODIGO_SWIFT        CHAR(11)     NOT NULL,
    PAIS_ID             INT          NOT NULL,

    CONSTRAINT PK_PA_ENTIDADES_BANCARIAS PRIMARY KEY (ENTIDAD_BANCARIA_ID),

    CONSTRAINT FK_PA_ENTIDADES_BANCARIAS_PA_PAISES
        FOREIGN KEY (PAIS_ID)
        REFERENCES dbo.PA_PAISES (PAIS_ID)
);
GO

-- =====================================================
-- Tabla dbo.PA_MONEDAS
-- =====================================================
IF OBJECT_ID(N'dbo.PA_MONEDAS', N'U') IS NOT NULL
    DROP TABLE dbo.PA_MONEDAS;
GO

CREATE TABLE dbo.PA_MONEDAS (
    MONEDA_ID   INT         IDENTITY(1,1) NOT NULL,
    NOMBRE      VARCHAR(45) NOT NULL,
    CODIGO_ISO  CHAR(3)     NOT NULL,
    SIMBOLO     CHAR(3)     NOT NULL,

    CONSTRAINT PK_PA_MONEDAS PRIMARY KEY (MONEDA_ID)
);
GO

-- =====================================================
-- Tabla dbo.PA_CUENTAS_ACREEDOR
-- =====================================================
IF OBJECT_ID(N'dbo.PA_CUENTAS_ACREEDOR', N'U') IS NOT NULL
    DROP TABLE dbo.PA_CUENTAS_ACREEDOR;
GO

CREATE TABLE dbo.PA_CUENTAS_ACREEDOR (
    CUENTA_ACREEDOR_ID   INT          IDENTITY(1,1) NOT NULL,
    TIPO_CUENTA          VARCHAR(45)  NOT NULL,
    NUMERO_CUENTA        VARCHAR(45)  NOT NULL,
    CCI                  VARCHAR(45)  NOT NULL,
    ACTIVO               CHAR(1)      NOT NULL CONSTRAINT DF_PA_CUENTAS_ACREEDOR_ACTIVO DEFAULT ('S'),
    ACREEDOR_ID          INT          NOT NULL,
    ENTIDAD_BANCARIA_ID  INT          NOT NULL,
    MONEDA_ID            INT          NOT NULL,
    FECHA_ELIMINACION    DATETIME2    NULL,
    USUARIO_ELIMINACION  INT          NULL,

    CONSTRAINT PK_PA_CUENTAS_ACREEDOR PRIMARY KEY (CUENTA_ACREEDOR_ID),
    CONSTRAINT UQ_PA_CUENTAS_ACREEDOR_NUMERO_CUENTA UNIQUE (NUMERO_CUENTA),

    CONSTRAINT FK_PA_CUENTAS_ACREEDOR_PA_ACREEDORES
        FOREIGN KEY (ACREEDOR_ID)
        REFERENCES dbo.PA_ACREEDORES (ACREEDOR_ID),

    CONSTRAINT FK_PA_CUENTAS_ACREEDOR_PA_ENTIDADES_BANCARIAS
        FOREIGN KEY (ENTIDAD_BANCARIA_ID)
        REFERENCES dbo.PA_ENTIDADES_BANCARIAS (ENTIDAD_BANCARIA_ID),

    CONSTRAINT FK_PA_CUENTAS_ACREEDOR_PA_MONEDAS
        FOREIGN KEY (MONEDA_ID)
        REFERENCES dbo.PA_MONEDAS (MONEDA_ID),

    CONSTRAINT FK_PA_CUENTAS_ACREEDOR_PA_USUARIOS
        FOREIGN KEY (USUARIO_ELIMINACION)
        REFERENCES dbo.PA_USUARIOS (USUARIO_ID)
);
GO

-- =====================================================
-- Tabla dbo.PA_CUENTAS_PROPIAS
-- =====================================================
IF OBJECT_ID(N'dbo.PA_CUENTAS_PROPIAS', N'U') IS NOT NULL
    DROP TABLE dbo.PA_CUENTAS_PROPIAS;
GO

CREATE TABLE dbo.PA_CUENTAS_PROPIAS (
    CUENTA_PROPIA_ID     INT          IDENTITY(1,1) NOT NULL,
    SALDO_DISPONIBLE     DECIMAL(15,2) NOT NULL CONSTRAINT DF_PA_CUENTAS_PROPIAS_SALDO_DISPONIBLE DEFAULT (0.0),
    TIPO_CUENTA          VARCHAR(45)  NOT NULL,
    NUMERO_CUENTA        VARCHAR(45)  NOT NULL,
    CCI                  VARCHAR(45)  NOT NULL,
    ACTIVO               CHAR(1)      NOT NULL CONSTRAINT DF_PA_CUENTAS_PROPIAS_ACTIVO DEFAULT ('S'),
    ENTIDAD_BANCARIA_ID  INT          NOT NULL,
    MONEDA_ID            INT          NOT NULL,
    FECHA_ELIMINACION    DATETIME2    NULL,
    USUARIO_ELIMINACION  INT          NULL,

    CONSTRAINT PK_PA_CUENTAS_PROPIAS PRIMARY KEY (CUENTA_PROPIA_ID),
    CONSTRAINT UQ_PA_CUENTAS_PROPIAS_NUMERO_CUENTA UNIQUE (NUMERO_CUENTA),

    CONSTRAINT FK_PA_CUENTAS_PROPIAS_PA_ENTIDADES_BANCARIAS
        FOREIGN KEY (ENTIDAD_BANCARIA_ID)
        REFERENCES dbo.PA_ENTIDADES_BANCARIAS (ENTIDAD_BANCARIA_ID),

    CONSTRAINT FK_PA_CUENTAS_PROPIAS_PA_MONEDAS
        FOREIGN KEY (MONEDA_ID)
        REFERENCES dbo.PA_MONEDAS (MONEDA_ID),

    CONSTRAINT FK_PA_CUENTAS_PROPIAS_PA_USUARIOS
        FOREIGN KEY (USUARIO_ELIMINACION)
        REFERENCES dbo.PA_USUARIOS (USUARIO_ID)
);
GO

USE [softpacbd];
GO

/* =====================================================
   PA_FACTURAS
   ===================================================== */
IF OBJECT_ID(N'dbo.PA_FACTURAS', N'U') IS NOT NULL
    DROP TABLE dbo.PA_FACTURAS;
GO

CREATE TABLE dbo.PA_FACTURAS (
    FACTURA_ID          INT           IDENTITY(1,1) NOT NULL,
    NUMERO_FACTURA      VARCHAR(45)   NOT NULL,
    FECHA_EMISION       DATETIME2     NOT NULL,
    FECHA_RECEPCION     DATETIME2     NOT NULL,
    FECHA_LIMITE_PAGO   DATETIME2     NOT NULL,
    ESTADO              VARCHAR(25)   NOT NULL 
                                      CONSTRAINT DF_PA_FACTURAS_ESTADO DEFAULT ('PENDIENTE'),
    MONTO_TOTAL         DECIMAL(15,2) NOT NULL,
    MONTO_IGV           DECIMAL(15,2) NOT NULL,
    MONTO_RESTANTE      DECIMAL(15,2) NOT NULL,
    REGIMEN_FISCAL      VARCHAR(30)   NOT NULL,
    TASA_IVA            DECIMAL(15,2) NULL,
    OTROS_TRIBUTOS      DECIMAL(15,2) NULL,
    MONEDA_ID           INT           NOT NULL,
    ACREEDOR_ID         INT           NOT NULL,
    FECHA_ELIMINACION   DATETIME2     NULL,
    USUARIO_ELIMINACION INT           NULL,

    CONSTRAINT PK_PA_FACTURAS PRIMARY KEY (FACTURA_ID),

    CONSTRAINT UQ_PA_FACTURAS_NUMERO_FACTURA
        UNIQUE (NUMERO_FACTURA),

    CONSTRAINT FK_PA_FACTURAS_PA_ACREEDORES
        FOREIGN KEY (ACREEDOR_ID)
        REFERENCES dbo.PA_ACREEDORES (ACREEDOR_ID),

    CONSTRAINT FK_PA_FACTURAS_PA_MONEDAS
        FOREIGN KEY (MONEDA_ID)
        REFERENCES dbo.PA_MONEDAS (MONEDA_ID),

    CONSTRAINT FK_PA_FACTURAS_PA_USUARIOS
        FOREIGN KEY (USUARIO_ELIMINACION)
        REFERENCES dbo.PA_USUARIOS (USUARIO_ID)
);
GO

/* =====================================================
   PA_PROPUESTAS_DE_PAGO
   ===================================================== */
IF OBJECT_ID(N'dbo.PA_PROPUESTAS_DE_PAGO', N'U') IS NOT NULL
    DROP TABLE dbo.PA_PROPUESTAS_DE_PAGO;
GO

CREATE TABLE dbo.PA_PROPUESTAS_DE_PAGO (
    PROPUESTA_DE_PAGO_ID     INT           IDENTITY(1,1) NOT NULL,
    FECHA_HORA_CREACION      DATETIME2     NOT NULL,
    ESTADO                   VARCHAR(20)   NOT NULL 
                                          CONSTRAINT DF_PA_PROPUESTAS_DE_PAGO_ESTADO DEFAULT ('PENDIENTE'),
    ENTIDAD_BANCARIA_ID      INT           NOT NULL,
    FECHA_HORA_MODIFICACION  DATETIME2     NOT NULL,
    USUARIO_MODIFICACION     INT           NOT NULL,
    USUARIO_CREACION         INT           NOT NULL,
    FECHA_ELIMINACION        DATETIME2     NULL,
    USUARIO_ELIMINACION      INT           NULL,

    CONSTRAINT PK_PA_PROPUESTAS_DE_PAGO PRIMARY KEY (PROPUESTA_DE_PAGO_ID),

    CONSTRAINT FK_PA_PROPUESTAS_DE_PAGO_PA_ENTIDADES_BANCARIAS
        FOREIGN KEY (ENTIDAD_BANCARIA_ID)
        REFERENCES dbo.PA_ENTIDADES_BANCARIAS (ENTIDAD_BANCARIA_ID),

    CONSTRAINT FK_PA_PROPUESTAS_DE_PAGO_PA_USUARIOS1
        FOREIGN KEY (USUARIO_ELIMINACION)
        REFERENCES dbo.PA_USUARIOS (USUARIO_ID),

    CONSTRAINT FK_PA_PROPUESTAS_DE_PAGO_PA_USUARIOS2
        FOREIGN KEY (USUARIO_MODIFICACION)
        REFERENCES dbo.PA_USUARIOS (USUARIO_ID),

    CONSTRAINT FK_PA_PROPUESTAS_DE_PAGO_PA_USUARIOS3
        FOREIGN KEY (USUARIO_CREACION)
        REFERENCES dbo.PA_USUARIOS (USUARIO_ID)
);
GO

/* =====================================================
   PA_DETALLES_PROPUESTA
   ===================================================== */
IF OBJECT_ID(N'dbo.PA_DETALLES_PROPUESTA', N'U') IS NOT NULL
    DROP TABLE dbo.PA_DETALLES_PROPUESTA;
GO

CREATE TABLE dbo.PA_DETALLES_PROPUESTA (
    DETALLE_PROPUESTA_ID  INT           IDENTITY(1,1) NOT NULL,
    MONTO_PAGO            DECIMAL(15,2) NOT NULL,
    FORMA_PAGO            CHAR(1)       NOT NULL,
    PROPUESTA_DE_PAGO_ID  INT           NOT NULL,
    FACTURA_ID            INT           NOT NULL,
    CUENTA_ACREEDOR_ID    INT           NOT NULL,
    CUENTA_PROPIA_ID      INT           NOT NULL,
    FECHA_ELIMINACION     DATETIME2     NULL,
    USUARIO_ELIMINACION   INT           NULL,
    ESTADO                VARCHAR(15)   NOT NULL 
                                       CONSTRAINT DF_PA_DETALLES_PROPUESTA_ESTADO DEFAULT ('Pendiente'),

    CONSTRAINT PK_PA_DETALLES_PROPUESTA PRIMARY KEY (DETALLE_PROPUESTA_ID),

    CONSTRAINT FK_PA_DETALLES_PROPUESTA_PA_PROPUESTAS_DE_PAGO
        FOREIGN KEY (PROPUESTA_DE_PAGO_ID)
        REFERENCES dbo.PA_PROPUESTAS_DE_PAGO (PROPUESTA_DE_PAGO_ID),

    CONSTRAINT FK_PA_DETALLES_PROPUESTA_PA_FACTURAS
        FOREIGN KEY (FACTURA_ID)
        REFERENCES dbo.PA_FACTURAS (FACTURA_ID),

    CONSTRAINT FK_PA_DETALLES_PROPUESTA_PA_CUENTAS_ACREEDOR
        FOREIGN KEY (CUENTA_ACREEDOR_ID)
        REFERENCES dbo.PA_CUENTAS_ACREEDOR (CUENTA_ACREEDOR_ID),

    CONSTRAINT FK_PA_DETALLES_PROPUESTA_PA_CUENTAS_PROPIAS
        FOREIGN KEY (CUENTA_PROPIA_ID)
        REFERENCES dbo.PA_CUENTAS_PROPIAS (CUENTA_PROPIA_ID),

    CONSTRAINT FK_PA_DETALLES_PROPUESTA_PA_USUARIOS
        FOREIGN KEY (USUARIO_ELIMINACION)
        REFERENCES dbo.PA_USUARIOS (USUARIO_ID)
);
GO

/* =====================================================
   PA_DETALLES_FACTURA
   ===================================================== */
IF OBJECT_ID(N'dbo.PA_DETALLES_FACTURA', N'U') IS NOT NULL
    DROP TABLE dbo.PA_DETALLES_FACTURA;
GO

CREATE TABLE dbo.PA_DETALLES_FACTURA (
    DETALLE_FACTURA_ID   INT           IDENTITY(1,1) NOT NULL,
    SUBTOTAL             DECIMAL(15,2) NOT NULL,
    DESCRIPCION          VARCHAR(125)  NOT NULL,
    FACTURA_ID           INT           NOT NULL,
    FECHA_ELIMINACION    DATETIME2     NULL,
    USUARIO_ELIMINACION  INT           NULL,

    CONSTRAINT PK_PA_DETALLES_FACTURA PRIMARY KEY (DETALLE_FACTURA_ID),

    CONSTRAINT FK_PA_DETALLES_FACTURA_PA_FACTURAS
        FOREIGN KEY (FACTURA_ID)
        REFERENCES dbo.PA_FACTURAS (FACTURA_ID),

    CONSTRAINT FK_PA_DETALLES_FACTURA_PA_USUARIOS
        FOREIGN KEY (USUARIO_ELIMINACION)
        REFERENCES dbo.PA_USUARIOS (USUARIO_ID)
);
GO

/* =====================================================
   PA_REPORTES_FACTURAS_PENDIENTES
   ===================================================== */
IF OBJECT_ID(N'dbo.PA_REPORTES_FACTURAS_PENDIENTES', N'U') IS NOT NULL
    DROP TABLE dbo.PA_REPORTES_FACTURAS_PENDIENTES;
GO

CREATE TABLE dbo.PA_REPORTES_FACTURAS_PENDIENTES (
    FACTURA_ID        INT           NOT NULL,
    ACREEDOR_ID       INT           NOT NULL,
    PAIS_ID           INT           NOT NULL,
    MONEDA_ID         INT           NOT NULL,
    NUMERO_FACTURA    VARCHAR(45)   NOT NULL,
    RAZON_SOCIAL      VARCHAR(200)  NOT NULL,
    NOMBRE_PAIS       VARCHAR(60)   NOT NULL,
    CODIGO_MONEDA     CHAR(3)       NOT NULL,
    FECHA_LIMITE_PAGO DATETIME2     NOT NULL,
    DIAS_VENCIMIENTO  INT           NOT NULL,
    RANGO_VENCIMIENTO VARCHAR(10)   NOT NULL,
    MONTO_RESTANTE    DECIMAL(15,2) NOT NULL,

    CONSTRAINT PK_PA_REPORTES_FACTURAS_PENDIENTES
        PRIMARY KEY (FACTURA_ID, ACREEDOR_ID),

    CONSTRAINT FK_PA_REPORTES_FACTURAS_PENDIENTES_PA_FACTURAS
        FOREIGN KEY (FACTURA_ID)
        REFERENCES dbo.PA_FACTURAS (FACTURA_ID),

    CONSTRAINT FK_PA_REPORTES_FACTURAS_PENDIENTES_PA_ACREEDORES
        FOREIGN KEY (ACREEDOR_ID)
        REFERENCES dbo.PA_ACREEDORES (ACREEDOR_ID),

    CONSTRAINT FK_PA_REPORTES_FACTURAS_PENDIENTES_PA_PAISES
        FOREIGN KEY (PAIS_ID)
        REFERENCES dbo.PA_PAISES (PAIS_ID),

    CONSTRAINT FK_PA_REPORTES_FACTURAS_PENDIENTES_PA_MONEDAS
        FOREIGN KEY (MONEDA_ID)
        REFERENCES dbo.PA_MONEDAS (MONEDA_ID)
);
GO

/* =====================================================
   PA_ESTADO_CUENTAS_PROPIAS
   ===================================================== */
IF OBJECT_ID(N'dbo.PA_ESTADO_CUENTAS_PROPIAS', N'U') IS NOT NULL
    DROP TABLE dbo.PA_ESTADO_CUENTAS_PROPIAS;
GO

CREATE TABLE dbo.PA_ESTADO_CUENTAS_PROPIAS (
    CUENTA_PROPIA_ID      INT           NOT NULL,
    FECHA_MODIFICACION    DATETIME2     NOT NULL,
    SALDO_MODIFICACION    DECIMAL(15,2) NOT NULL,
    SALDO_RESULTANTE      DECIMAL(15,2) NOT NULL,
    USUARIO_MODIFICACION  INT           NOT NULL,

    CONSTRAINT FK_PA_ESTADO_CUENTAS_PROPIAS_PA_USUARIOS
        FOREIGN KEY (USUARIO_MODIFICACION)
        REFERENCES dbo.PA_USUARIOS (USUARIO_ID),

    CONSTRAINT FK_PA_ESTADO_CUENTAS_PROPIAS_PA_CUENTAS_PROPIAS
        FOREIGN KEY (CUENTA_PROPIA_ID)
        REFERENCES dbo.PA_CUENTAS_PROPIAS (CUENTA_PROPIA_ID)
);
GO

USE [softpacbd];
GO

/* =====================================================
   VW_FACTURA_COMPLETA
   ===================================================== */
IF OBJECT_ID(N'dbo.VW_FACTURA_COMPLETA', N'V') IS NOT NULL
    DROP VIEW dbo.VW_FACTURA_COMPLETA;
GO

CREATE VIEW dbo.VW_FACTURA_COMPLETA AS
SELECT
  /*  1 */ f.FACTURA_ID                          AS FACTURA_ID,
  /*  2 */ f.NUMERO_FACTURA                      AS NUMERO_FACTURA,
  /*  3 */ f.FECHA_EMISION                       AS FECHA_EMISION,
  /*  4 */ f.FECHA_RECEPCION                     AS FECHA_RECEPCION,
  /*  5 */ f.FECHA_LIMITE_PAGO                   AS FECHA_LIMITE_PAGO,
  /*  6 */ f.ESTADO                              AS ESTADO,
  /*  7 */ f.MONTO_TOTAL                         AS MONTO_TOTAL,
  /*  8 */ f.MONTO_IGV                           AS MONTO_IGV,
  /*  9 */ f.MONTO_RESTANTE                      AS MONTO_RESTANTE,
  /* 10 */ f.REGIMEN_FISCAL                      AS REGIMEN_FISCAL,
  /* 11 */ f.TASA_IVA                            AS TASA_IVA,          
  /* 12 */ f.OTROS_TRIBUTOS                      AS OTROS_TRIBUTOS,
  /* Moneda */
  /* 13 */ m.MONEDA_ID                           AS MONEDA_ID,
  /* 14 */ m.NOMBRE                              AS MONEDA_NOMBRE,
  /* 15 */ m.CODIGO_ISO                          AS MONEDA_CODIGO_ISO,
  /* 16 */ m.SIMBOLO                             AS MONEDA_SIMBOLO,
  /* Acreedor */
  /* 17 */ a.ACREEDOR_ID                         AS ACREEDOR_ID,
  /* 18 */ a.RAZON_SOCIAL                        AS ACREEDOR_RAZON_SOCIAL,
  /* 19 */ a.RUC                                 AS ACREEDOR_RUC,
  /* 20 */ a.DIRECCION_FISCAL                    AS ACREEDOR_DIRECCION_FISCAL,
  /* 21 */ a.CONDICION                           AS ACREEDOR_CONDICION,
  /* 22 */ a.PLAZO_DE_PAGO                       AS ACREEDOR_PLAZO_DE_PAGO,
  /* 23 */ a.ACTIVO                              AS ACREEDOR_ACTIVO,
  /* PaÃ­s del acreedor */
  /* 24 */ p.PAIS_ID                             AS PAIS_ID,
  /* 25 */ p.NOMBRE                              AS PAIS_NOMBRE,
  /* Detalles (pueden ser NULL si no hay detalles) */
  /* 26 */ df.DETALLE_FACTURA_ID                 AS DETALLE_FACTURA_ID,
  /* 27 */ df.SUBTOTAL                           AS DETALLE_SUBTOTAL,
  /* 28 */ df.DESCRIPCION                        AS DETALLE_DESCRIPCION
FROM PA_FACTURAS f
JOIN PA_MONEDAS        m  ON m.MONEDA_ID   = f.MONEDA_ID
JOIN PA_ACREEDORES     a  ON a.ACREEDOR_ID = f.ACREEDOR_ID
JOIN PA_PAISES         p  ON p.PAIS_ID     = a.PAIS_ID
LEFT JOIN PA_DETALLES_FACTURA df 
    ON df.FACTURA_ID = f.FACTURA_ID 
   AND df.FECHA_ELIMINACION IS NULL;
GO

/* =====================================================
   VW_PROPUESTA_COMPLETA
   ===================================================== */
IF OBJECT_ID(N'dbo.VW_PROPUESTA_COMPLETA', N'V') IS NOT NULL
    DROP VIEW dbo.VW_PROPUESTA_COMPLETA;
GO

CREATE VIEW dbo.VW_PROPUESTA_COMPLETA AS
SELECT 
    -- Propuesta
    p.PROPUESTA_DE_PAGO_ID         AS P1_PROPUESTA_DE_PAGO_ID,
    p.FECHA_HORA_CREACION          AS P2_FECHA_HORA_CREACION,
    p.FECHA_HORA_MODIFICACION      AS P3_FECHA_HORA_MODIFICACION,
    p.USUARIO_CREACION             AS P4_USUARIO_CREACION,
    uc.CORREO_ELECTRONICO          AS P5_USUARIO_CREACION_CORREO,
    uc.NOMBRE_DE_USUARIO           AS P6_USUARIO_CREACION_NOMBRE_USUARIO,
    uc.NOMBRE                      AS P7_USUARIO_CREACION_NOMBRE,
    uc.APELLIDOS                   AS P8_USUARIO_CREACION_APELLIDOS,
    uc.ACTIVO                      AS P9_USUARIO_CREACION_ACTIVO,
    uc.SUPERUSUARIO                AS P10_USUARIO_CREACION_SUPERUSUARIO,
    p.USUARIO_MODIFICACION         AS P11_USUARIO_MODIFICACION,
    um.CORREO_ELECTRONICO          AS P12_USUARIO_MODIFICACION_CORREO,
    um.NOMBRE_DE_USUARIO           AS P13_USUARIO_MODIFICACION_NOMBRE_USUARIO,
    um.NOMBRE                      AS P14_USUARIO_MODIFICACION_NOMBRE,
    um.APELLIDOS                   AS P15_USUARIO_MODIFICACION_APELLIDOS,
    um.ACTIVO                      AS P16_USUARIO_MODIFICACION_ACTIVO,
    um.SUPERUSUARIO                AS P17_USUARIO_MODIFICACION_SUPERUSUARIO,
    p.ESTADO                       AS P18_ESTADO_PROPUESTA,
    p.ENTIDAD_BANCARIA_ID          AS P19_ENTIDAD_BANCARIA_PROPUESTA,
    ebp.NOMBRE                     AS P20_ENTIDAD_BANCARIA_NOMBRE,
    ebp.FORMATO_ACEPTADO           AS P21_ENTIDAD_BANCARIA_FORMATO,
    ebp.CODIGO_SWIFT               AS P22_ENTIDAD_BANCARIA_SWIFT,
    ebp.PAIS_ID                    AS P23_ENTIDAD_BANCARIA_PAIS_ID,
    pbp.NOMBRE                     AS P24_ENTIDAD_BANCARIA_PAIS_NOMBRE,

    -- Detalle de propuesta
    dp.DETALLE_PROPUESTA_ID        AS D1_DETALLE_PROPUESTA_ID,
    dp.MONTO_PAGO                  AS D2_MONTO_PAGO,
    dp.FORMA_PAGO                  AS D3_FORMA_PAGO,
    dp.ESTADO                      AS D4_ESTADO,

    -- Factura
    f.FACTURA_ID                   AS F1_FACTURA_ID,
    f.NUMERO_FACTURA               AS F2_NUMERO_FACTURA,
    f.FECHA_EMISION                AS F3_FECHA_EMISION,
    f.FECHA_RECEPCION              AS F4_FECHA_RECEPCION,
    f.FECHA_LIMITE_PAGO            AS F5_FECHA_LIMITE_PAGO,
    f.ESTADO                       AS F6_ESTADO_FACTURA,
    f.MONTO_TOTAL                  AS F7_MONTO_TOTAL,
    f.MONTO_IGV                    AS F8_MONTO_IGV,
    f.MONTO_RESTANTE               AS F9_MONTO_RESTANTE,
    f.REGIMEN_FISCAL               AS F10_REGIMEN_FISCAL,
    f.TASA_IVA                     AS F11_TASA_IVA,
    f.OTROS_TRIBUTOS               AS F12_OTROS_TRIBUTOS,
    f.MONEDA_ID                    AS F13_MONEDA_ID,
    f.ACREEDOR_ID                  AS F14_ACREEDOR_ID,

    -- Cuenta acreedor (destino)
    ca.CUENTA_ACREEDOR_ID          AS CA1_CUENTA_ACREEDOR_ID,
    ca.TIPO_CUENTA                 AS CA2_TIPO_CUENTA,
    ca.NUMERO_CUENTA               AS CA3_NUMERO_CUENTA,
    ca.CCI                         AS CA4_CCI,
    ca.ACTIVO                      AS CA5_ACTIVO,
    ca.ACREEDOR_ID                 AS CA6_ACREEDOR_ID,
    ca.ENTIDAD_BANCARIA_ID         AS CA7_ENTIDAD_BANCARIA_ID,
    ca.MONEDA_ID                   AS CA8_MONEDA_ID,
    eba.NOMBRE                     AS CA9_ENTIDAD_BANCARIA_NOMBRE,
    eba.CODIGO_SWIFT               AS CA10_ENTIDAD_BANCARIA_SWIFT,
    eba.PAIS_ID                    AS CA11_ENTIDAD_BANCARIA_PAIS_ID,
    pba.NOMBRE                     AS CA12_ENTIDAD_BANCARIA_PAIS_NOMBRE,

    -- Cuenta propia (origen)
    cp.CUENTA_PROPIA_ID            AS CP1_CUENTA_PROPIA_ID,
    cp.SALDO_DISPONIBLE            AS CP2_SALDO_DISPONIBLE,
    cp.TIPO_CUENTA                 AS CP3_TIPO_CUENTA,
    cp.NUMERO_CUENTA               AS CP4_NUMERO_CUENTA,
    cp.CCI                         AS CP5_CCI,
    cp.ACTIVO                      AS CP6_ACTIVO,
    cp.ENTIDAD_BANCARIA_ID         AS CP7_ENTIDAD_BANCARIA_ID,
    cp.MONEDA_ID                   AS CP8_MONEDA_ID,
    ebo.NOMBRE                     AS CP9_ENTIDAD_BANCARIA_NOMBRE,
    ebo.CODIGO_SWIFT               AS CP10_ENTIDAD_BANCARIA_SWIFT,
    ebo.PAIS_ID                    AS CP11_ENTIDAD_BANCARIA_PAIS_ID,
    pbo.NOMBRE                     AS CP12_ENTIDAD_BANCARIA_PAIS_NOMBRE,

    -- Acreedor y moneda de la factura
    ac.RAZON_SOCIAL                AS AC2_RAZON_SOCIAL_ACREEDOR,
    mo.CODIGO_ISO                  AS MO1_MONEDA_CODIGO_ISO
FROM PA_PROPUESTAS_DE_PAGO p
JOIN PA_USUARIOS uc 
    ON uc.USUARIO_ID = p.USUARIO_CREACION
JOIN PA_USUARIOS um 
    ON um.USUARIO_ID = p.USUARIO_MODIFICACION
JOIN PA_ENTIDADES_BANCARIAS ebp 
    ON ebp.ENTIDAD_BANCARIA_ID = p.ENTIDAD_BANCARIA_ID
JOIN PA_PAISES pbp 
    ON pbp.PAIS_ID = ebp.PAIS_ID
LEFT JOIN PA_DETALLES_PROPUESTA dp 
    ON dp.PROPUESTA_DE_PAGO_ID = p.PROPUESTA_DE_PAGO_ID
   AND dp.FECHA_ELIMINACION IS NULL
LEFT JOIN PA_FACTURAS f 
    ON f.FACTURA_ID = dp.FACTURA_ID
LEFT JOIN PA_ACREEDORES ac 
    ON ac.ACREEDOR_ID = f.ACREEDOR_ID
LEFT JOIN PA_MONEDAS mo 
    ON mo.MONEDA_ID = f.MONEDA_ID
LEFT JOIN PA_CUENTAS_ACREEDOR ca 
    ON ca.CUENTA_ACREEDOR_ID = dp.CUENTA_ACREEDOR_ID
LEFT JOIN PA_ENTIDADES_BANCARIAS eba 
    ON eba.ENTIDAD_BANCARIA_ID = ca.ENTIDAD_BANCARIA_ID
LEFT JOIN PA_PAISES pba 
    ON pba.PAIS_ID = eba.PAIS_ID
LEFT JOIN PA_CUENTAS_PROPIAS cp 
    ON cp.CUENTA_PROPIA_ID = dp.CUENTA_PROPIA_ID
LEFT JOIN PA_ENTIDADES_BANCARIAS ebo 
    ON ebo.ENTIDAD_BANCARIA_ID = cp.ENTIDAD_BANCARIA_ID
LEFT JOIN PA_PAISES pbo 
    ON pbo.PAIS_ID = ebo.PAIS_ID;
GO

USE softpacbd;
GO


/* =====================================================
   PROCEDURE pa_sp_reporte_facturas_pendientes
   ===================================================== */


IF EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'dbo.pa_sp_reporte_facturas_pendientes') AND type = 'P')
BEGIN
    DROP PROCEDURE dbo.pa_sp_reporte_facturas_pendientes;
END
GO

CREATE PROCEDURE dbo.pa_sp_reporte_facturas_pendientes
    @p_acreedor_id INT = 0,   -- 0 = sin filtro
    @p_pais_id INT = 0,       -- 0 = sin filtro
    @p_moneda_id INT = 0      -- 0 = sin filtro
AS
BEGIN
    SET NOCOUNT ON;
    
    BEGIN TRY
        -- Eliminar registros existentes que cumplan los filtros
        DELETE FROM PA_REPORTES_FACTURAS_PENDIENTES
        WHERE (@p_acreedor_id = 0 OR ACREEDOR_ID = @p_acreedor_id)
          AND (@p_pais_id = 0 OR PAIS_ID = @p_pais_id)
          AND (@p_moneda_id = 0 OR MONEDA_ID = @p_moneda_id);

        -- Insertar nuevos registros
        INSERT INTO PA_REPORTES_FACTURAS_PENDIENTES (
            FACTURA_ID, ACREEDOR_ID, PAIS_ID, MONEDA_ID,
            NUMERO_FACTURA, RAZON_SOCIAL, NOMBRE_PAIS, CODIGO_MONEDA,
            FECHA_LIMITE_PAGO, DIAS_VENCIMIENTO, RANGO_VENCIMIENTO, MONTO_RESTANTE
        )
        SELECT
            f.FACTURA_ID,
            a.ACREEDOR_ID,
            pa.PAIS_ID,
            f.MONEDA_ID,
            f.NUMERO_FACTURA,
            a.RAZON_SOCIAL,
            pa.NOMBRE,
            m.CODIGO_ISO,
            f.FECHA_LIMITE_PAGO,
            DATEDIFF(DAY, CAST(GETDATE() AS DATE), f.FECHA_LIMITE_PAGO),
            CASE
                WHEN DATEDIFF(DAY, CAST(GETDATE() AS DATE), f.FECHA_LIMITE_PAGO) BETWEEN 0 AND 30 THEN '0-30'
                WHEN DATEDIFF(DAY, CAST(GETDATE() AS DATE), f.FECHA_LIMITE_PAGO) BETWEEN 31 AND 60 THEN '31-60'
                WHEN DATEDIFF(DAY, CAST(GETDATE() AS DATE), f.FECHA_LIMITE_PAGO) BETWEEN 61 AND 90 THEN '61-90'
                ELSE '+90'
            END,
            f.MONTO_RESTANTE
        FROM PA_FACTURAS f
        INNER JOIN PA_ACREEDORES a ON a.ACREEDOR_ID = f.ACREEDOR_ID
        INNER JOIN PA_MONEDAS m ON m.MONEDA_ID = f.MONEDA_ID
        INNER JOIN PA_PAISES pa ON pa.PAIS_ID = a.PAIS_ID
        WHERE f.ESTADO = 'PENDIENTE'
          AND f.FECHA_ELIMINACION IS NULL
          AND DATEDIFF(DAY, CAST(GETDATE() AS DATE), f.FECHA_LIMITE_PAGO) >= 0
          AND (@p_acreedor_id = 0 OR a.ACREEDOR_ID = @p_acreedor_id)
          AND (@p_pais_id = 0 OR pa.PAIS_ID = @p_pais_id)
          AND (@p_moneda_id = 0 OR f.MONEDA_ID = @p_moneda_id);
          
    END TRY
    BEGIN CATCH
        -- Mostrar error si algo falla
        SELECT 
            ERROR_NUMBER() AS ErrorNumber,
            ERROR_MESSAGE() AS ErrorMessage,
            ERROR_LINE() AS ErrorLine;
        THROW;
    END CATCH
END;
GO
